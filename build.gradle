plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.gradle.plugin-publish" version "0.9.1"
    id "de.kotka.project" version "1.5.1"
}

apply plugin: 'groovy'

group = 'de.kotka.clojuresque'
version = '1.8.0-SNAPSHOT'
description = "A Clojure plugin for gradle"

kotka {
    wrapperVersion = "2.7"
    licenses   = [ "MIT" ]
    repository = "clojuresque/clojuresque"
    bintray    = [ repo: "clojuresque" ]
}

ext.clojuresquePlugins = [
    base:    "2.0.0",
    nrepl:   "2.0.0",
    extras:  "2.0.0"
]

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    project.clojuresquePlugins.each { plugin, version ->
        compile "de.kotka.clojuresque:clojuresque-${plugin}:${version}"
    }
}

compileGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}
